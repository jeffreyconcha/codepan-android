plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def secretsFile = rootProject.file("secrets.properties")
def secrets = new Properties()

if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
} else {
    logger.warn("⚠️ secrets.properties file not found in project root.")
}

def githubUser = secrets.getProperty("gpr.user") ?: ""
def githubToken = secrets.getProperty("gpr.token") ?: ""

android {
    compileSdk 35

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        renderscriptTargetApi 17
        renderscriptSupportModeEnabled false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    namespace 'com.codepan'

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
    }

    lintOptions {
        abortOnError false         // Don't fail the build on lint errors
        checkReleaseBuilds false   // Skip lint during release builds
        warningsAsErrors false     // Don't treat warnings as errors
    }
}


task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            create("release", MavenPublication) {
                from components.release
                groupId = 'com.codepan'
                artifactId = 'codepan-android'
                version = '1.8.4'
            }
        }

        repositories {
            maven {
                name = "GithubPackages"
                url = uri("https://maven.pkg.github.com/jeffreyconcha/codepan-android")
                credentials {
                    username = githubUser
                    password = githubToken
                }
            }
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'net.zetetic:sqlcipher-android:4.10.0'
    api 'androidx.sqlite:sqlite-ktx:2.6.1'
    api 'com.github.Infomaniak:android-pdfview:3.2.13'
    api 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    api 'com.google.android.gms:play-services-location:21.3.0'
    api 'com.google.android.gms:play-services-maps:19.2.0'
    api 'com.google.maps.android:android-maps-utils:3.6.0'
    api 'androidx.recyclerview:recyclerview:1.4.0'
    api 'com.google.android.flexbox:flexbox:3.0.0'
    api "androidx.core:core-ktx:1.15.0"
    api 'com.google.mlkit:vision-common:17.3.0'
    api 'com.google.mlkit:text-recognition:16.0.1'
    api 'com.google.android.gms:play-services-mlkit-face-detection:17.1.0'
    def camerax_version = "1.5.1"
    api "androidx.camera:camera-core:${camerax_version}"
    api "androidx.camera:camera-camera2:${camerax_version}"
    api "androidx.camera:camera-lifecycle:${camerax_version}"
    api "androidx.camera:camera-video:${camerax_version}"
    api "androidx.camera:camera-view:${camerax_version}"
    api "androidx.camera:camera-extensions:${camerax_version}"
    api 'androidx.window:window:1.5.0'
    api 'androidx.concurrent:concurrent-futures-ktx:1.3.0'
    api "androidx.exifinterface:exifinterface:1.4.1"
    api "com.squareup.okhttp3:okhttp:4.12.0"
}